#!/usr/bin/env escript

%% run in the top directory of an erlang project, updates the OTP application resource file
%% to reflect the complete list of modules in the src directory. 
%%
%% Doesn't account for packages.
%% Stomps on your existing app file.

main(_Args) ->
    {ok, Pwd} = file:get_cwd(),
    {true, AppFile} = get_app_spec(Pwd),    
    {ok, AppData} = file:consult(AppFile),
    [{application, AppSpec, PList}] = AppData,
    Files = filelib:fold_files("src", ".*\\.erl$", true, fun(F, AccIn) -> [F|AccIn] end, []),
	Sources = 
		lists:map(
        	fun(Path) -> 
				list_to_atom(
				filename:basename(string:join(
					string:tokens(string:right(Path, length(Path) - 4), "/"), "."), ".erl"))
			end, 
		Files),
    NewAppData = {application, AppSpec, lists:keyreplace(modules, 1, PList, {modules, Sources})}, 
    io:format("Updating application modules: ~p~n", [Sources]),
    Repr = erl_pp:expr(erl_parse:abstract(NewAppData)),
    case file:write_file(AppFile, list_to_binary(Repr ++ ".")) of
        ok -> ok;
        {error, Err} -> io:format("~p~n", [Err]), halt(1)
    end.

get_app_spec(Dir) ->
    App = filename:join([Dir, "ebin/*.app"]),
    case filelib:wildcard(App) of
        [AppFile] ->
            {true, AppFile};
        _ ->
            false
    end.
